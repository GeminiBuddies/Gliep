fn counter[ctr] ( ctr = ctr - 1; fn -> ctr = ctr + 1 );
!a = counter[0];
!b = counter[7];

print$a[];
print$a[];
print$b[];
print$a[];

print $ (fn fibo[x] if (x == 0 | x == 1) x else fibo[x - 1] + fibo[x - 2]) 20;

############################################

fn beide -> [1, 2];
fn sum[x, y] x + y;

print$beide[];      # expected: 1 2
print[beide[]];     # expected: 1
print(beide[] - 1); # expected: 0
print$sum$beide[];  # expected: 3

############################################

Y = f -> (x -> f(x x))(x -> f(v -> x x v));
print $ Y (f -> n -> if (n == 0) 1 else n * f(n - 1)) 10;

print "ユニコードイグザンプル\u677e";

############################################
fn is_numeric[x] `type x == "integer" | `type x == "float";

vector = { 
    .__add: [a, b] -> vector.new[a.x + b.x, a.y + b.y],
    .__sub: [a, b] -> vector.new[a.x - b.x, a.y - b.y],
    .__mul: [a, b] -> if (is_numeric a) vector.new[a * b.x, a * b.y] 
                        elif (is_numeric b) vector.new[b * a.x, b * a.y] 
                        else a.x * b.x + a.y * b.y,
    .__lss: [a, b] -> vector.len2$a < vector.len2$b,
    .__equ: [a, b] -> a.x == b.x & a.y == b.y,
    .__neg: v -> vector.new[-(v.x), -(v.y)],

    .__str: v -> format["({0}, {1})", v.x, v.y],

    .new: [x, y] -> (rv = { .x: x, .y: y }; `meta(rv) = vector; rv),
    .len2: v -> v * v,
};


x = vector.new[1, 0];
y = vector.new[0, 1];
c = x * 1 + 2 * y;
d = x * 3 - 4 * -y;
e = x * 5;

print[c, d, e];

############################################

for (i: range[0, 10]) print i;

!vec = {| 9, 8, 7, 6 |};

for (j: iter vec) print j;
for (index, value: pairs vec) print[index, value]; 

